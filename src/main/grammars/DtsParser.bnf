{
    parserClass = "me.jkdhn.devicetree.parser.DtsParser"
    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"
    psiClassPrefix = "Dts"
    psiImplClassSuffix = "Impl"
    psiPackage = "me.jkdhn.devicetree.psi"
    psiImplPackage = "me.jkdhn.devicetree.psi.impl"
    elementTypeHolderClass = "me.jkdhn.devicetree.psi.DtsTypes"
    elementTypeClass = "me.jkdhn.devicetree.psi.DtsElementType"
    tokenTypeClass = "me.jkdhn.devicetree.psi.DtsTokenType"
    psiImplUtilClass = "me.jkdhn.devicetree.psi.impl.DtsPsiImplUtil"
    parserUtilClass = "me.jkdhn.devicetree.parser.DtsParserUtil"
    tokens = [
        HEADER_V1 = "/dts-v1/"
        BRACE_LEFT = "{"
        BRACE_RIGHT = "}"
        PAR_LEFT = "("
        PAR_RIGHT = ")"
        LT = "<"
        GT = ">"
        SEMICOLON = ";"
        AT = "@"
        EQ = "="
        AMPERSAND = "&"
        COLON = ":"
        ADD = "+"
        SUB = "-"
        MUL = "*"
        SLASH = "/"
        STRING = "<string>"
        INTEGER = "<int>"
        NODE_NAME = "<node name>"
        PROPERTY_NAME = "<property name>"
        LABEL_NAME = "<label name>"
    ]
}

File ::= Statement*

Statement ::= ((HEADER_V1 | TreeNode | PropertyDefinition) SEMICOLON)

LabelDefinition ::= LABEL_NAME COLON {
    mixin = "me.jkdhn.devicetree.psi.impl.DtsNamedElementImpl"
    implements = "me.jkdhn.devicetree.psi.DtsNamedElement"
    methods = [
        getNameIdentifier
        getName
        setName
    ]
}

LabelReference ::= AMPERSAND LABEL_NAME {
    methods = [
        getReference
    ]
}

TreeNode ::= ((LabelDefinition* TreeNodeName) | LabelReference) BRACE_LEFT Statement* BRACE_RIGHT
TreeNodeName ::= (NODE_NAME (AT UNIT_ADDRESS)?) | SLASH

PropertyDefinition ::= LabelDefinition* PROPERTY_NAME (EQ PropertyValue (COMMA PropertyValue)*)?
PropertyValue ::= LabelDefinition* (PropertyValueCell | STRING | LabelReference) LabelDefinition*
PropertyValueCell ::= LT PropertyValueCellEntry* LabelDefinition* GT
private PropertyValueCellEntry ::= LabelDefinition* (LabelReference | PropertyValueExpression)
PropertyValueExpression ::= INTEGER | (PAR_LEFT PropertyValueExpression PAR_RIGHT) | (PAR_LEFT PropertyValueExpression PropertyValueOperator PropertyValueExpression PAR_RIGHT)
PropertyValueOperator ::= ADD | SUB | MUL | SLASH
